#!/usr/bin/env -S just --one --unstable --justfile

set export
export api := require('secret-service-api')
export fzf_table := require('fzf-table')
export this := justfile()
export tui := 'auto'

# => default ------------------------------------------------------------------------------------------------------ {{{1

# Run TUI if interactive, otherwise print static view
[private]
default:
    #!/usr/bin/env -S bash -Eeuo pipefail
    if [[ "$tui" == 'force' ]] || [[ "$tui" == 'auto' && -t 1 ]]; then
        "{{this}}" controller
    else
        "{{this}}" view
    fi

# Print help
[script("/usr/bin/env", "-S", "colored-md")]
help:
    # ABSTRACT
    Interactive TUI for searching and managing secrets from the D-Bus Secret Service

    # DESCRIPTION
    `menu-secret-service` provides a fast, fuzzy-searchable interface for secrets stored via the standard
    Secret Service API (used by GNOME Keychain, KeePassXC, etc.)

    ## Key Features

    * **Fuzzy Search:** Instantly search through all secret labels and paths
    * **Clipboard Integration:** The primary action copies the selected secret to the clipboard
    * **Rich Preview:** The preview window shows all metadata for the selected secret

# => MVC ---------------------------------------------------------------------------------------------------------- {{{1

# List all secrets as a JSON stream
@model:
    "$api" list

# Print secrets as a formatted and colorized table
view:
    #!/usr/bin/env -S bash -Eeuo pipefail
    jq_fields=(
        'id;\(.path)'
        'label;\(.label);olive'
        'type;\(.content_type)'
    )
    "$this" model | json2table 'sort_by(.label)' "${jq_fields[@]}"

# Display the interactive TUI for managing secrets
controller:
    #!/usr/bin/env -S bash -Eeuo pipefail
    export FZF_RELOAD_CMD="${this@Q} view"
    transform_previw='.created |= "\(.) (\(. | todate))" | .modified |= "\(.) (\(. | todate))"'
    fzf_args=(
        --bind="alt-R:execute-silent(${api@Q} remove {1})+reload($FZF_RELOAD_CMD)"
        --bind="alt-i:execute(${api@Q} fetch {1} | json2yaml | colored-yaml --paging=always --file-name={1})"
        --bind="ctrl-m:become(${api@Q} fetch {1} | jq -r .secret | clipcopy)"
        --footer="A-i:inspect A-R:remove"
        --preview-window="right:50%:border-left:wrap:nohidden,<55(hidden)"
        --preview="${api@Q} fetch {1} | jq ${transform_previw@Q} | json2yaml | colored-yaml"
        --prompt="secrets > "
        --with-nth="2.."
    )
    "$this" view | "$fzf_table" "${fzf_args[@]}"
