#!/usr/bin/env -S just --one --unstable --justfile

# https://just.systems/man/en/

set export
export fzf-table-multi := require('fzf-table-multi')
export json2table := require('json2table')
export this := justfile()
export tui := 'auto'

# => default ------------------------------------------------------------------------------------------------------ {{{1

# Run TUI if interactive, otherwise print static view
[private]
default:
    #!/usr/bin/env -S bash -Eeuo pipefail
    if [[ "$tui" == 'force' ]] || [[ "$tui" == 'auto' && -t 1 ]]; then
        "$this" controller
    else
        "$this" view
    fi

# Print help
[script("/usr/bin/env", "-S", "colored-md")]
help:
    # ABSTRACT
    Interactive TUI for searching and managing pacman packages

    # DESCRIPTION
    `menu-pacman` provides a unified, interactive interface for the `pacman` package manager.

    It solves the problem of running multiple `pacman` commands to see what's available, what's installed, and
    whether a package is an explicit dependency. It combines all this information into a single, searchable, and
    actionable dashboard for full lifecycle package management.

    ## Key Features

    * **Unified View:** Merges the output of `pacman -Sl` (all packages) and `pacman -Qe` (explicitly installed)
      to provide a comprehensive view of the system's package state.
    * **Full Lifecycle Management:** Provides intuitive key bindings to install (`-S`), remove (`-Rcs`), upgrade
      (`-Syu`), and change the dependency status (`-D --asdeps/--asexplicit`) of packages.
    * **Informative Preview:** The preview window shows detailed package information (`pacman -Qi` or `-Si`) and lists
      the files owned by the package (`pacman -Ql` or `-Fl`).
    * **Clean & Composable:** Follows the three-mode pattern, allowing it to be used as an interactive TUI, a static
      table generator, or a pure JSON data source.

# => MVC ---------------------------------------------------------------------------------------------------------- {{{1

# Generate a unified JSON list of all available and installed packages
model:
    #!/usr/bin/env -S bash -Eeuo pipefail
    pacman -Sl | "$this" merge-pacman-data <(pacman -Qe)

# Print available and installed packages as a formatted and colorized table
view:
    #!/usr/bin/env -S bash -Eeuo pipefail
    jq_fields=(
        '_id;\(.name)'
        '_url;\("https://aur.archlinux.org/packages/\(.name)")'
        'name;\(.name)'
        'type;\(if .explicit then "explicit" else "" end);yellow'
        'status;\(if .installed then "installed" else "" end);purple'
        'repo;\(.repo);gray'
    )
    "$this" model | json2table 'sort_by((.explicit | not), (.installed | not), .name)' "${jq_fields[@]}"

# Display the interactive TUI for managing pacman packages
controller:
    #!/usr/bin/env -S bash -Eeuo pipefail
    export FZF_RELOAD_CMD="${this@Q} view"
    fzf_args=(
        --bind="alt-A:execute(fzf-run-reload -ft 1 -- sudo pacman -S {+1})"
        --bind="alt-C:execute(fzf-run-reload -f    -- arch-remove-orphans)"
        --bind="alt-D:execute(fzf-run-reload -ft 1 -- sudo pacman -D --asdeps {+1})"
        --bind="alt-E:execute(fzf-run-reload -ft 1 -- sudo pacman -D --asexplicit {+1})"
        --bind="alt-R:execute(fzf-run-reload -ft 1 -- sudo pacman -Rcs {+1})"
        --bind="alt-U:execute(echo 'trizen -Syu --noconfirm --noedit --needed && sudo pacman -Fy' | fzf-run-reload -ft 1)"
        --bind="alt-i:execute(if [[ {5} == 'installed' ]]; then pacman -Ql {1}; else pacman -Fl {1}; fi | awk '{print \$2}' | ${PAGER:-less})"
        --bind="ctrl-m:become(echo {+1})"
        --footer="A-A:install A-C:clean A-D:asdep A-E:asexp A-R:remove A-U:upgrade A-i:inspect"
        --preview="if [[ {5} == 'installed' ]]; then cat <(pacman -Qi {1}) <(pacman -Ql {1} | awk '{print \$2}'); else cat <(pacman -Si {1}) <(pacman -Fl {1} | awk '{print \$2}'); fi"
        --preview-window="right:50%:border-left:wrap:nohidden,<55(hidden)"
        --prompt="pacman > "
        --with-nth="3.."
    )
    "$this" view | fzf-table-multi "${fzf_args[@]}"

# => helper ------------------------------------------------------------------------------------------------------- {{{1

# Merge `pacman -Sl` and `pacman -Qe` output into a single JSON stream
[private, no-cd]
merge-pacman-data explicit:
    #!/usr/bin/env -S perl -CALS -lan
    use v5.40;

    use Cpanel::JSON::XS;

    use constant {
        JSON_ONE_LINE => Cpanel::JSON::XS->new->canonical->utf8(0)->unblessed_bool([1])->pretty(0)->indent(0)
    };

    our (%e, @r);
    INIT {
        push @ARGV, q({{explicit}});
        %e = map {chomp; (split)} <<>>;
    }

    push @r, {repo => $F[0], name => $F[1], version => $F[2], installed => !!$F[3], explicit => !!(delete $e{$F[1]})};

    END {
        push @r, {repo => 'aur', name => $_, version => $e{$_}, installed => true, explicit => true} foreach keys %e;
        say JSON_ONE_LINE()->encode(\@r)
    }
