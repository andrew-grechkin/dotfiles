#!/usr/bin/env -S just --one --unstable --justfile

# https://just.systems/man/en/

set export
export colored-md := require('colored-md')
export fzf_table := require('fzf-table')
export json2table := require('json2table')
export this := justfile()
export tui := 'auto'

# => default ------------------------------------------------------------------------------------------------------ {{{1

# Run TUI if interactive, otherwise print static view
[private]
default:
    #!/usr/bin/env -S bash -Eeuo pipefail
    if [[ "$tui" == 'force' ]] || [[ "$tui" == 'auto' && -t 1 ]]; then
        "$this" controller
    else
        "$this" view
    fi

# Print help
[script("/usr/bin/env", "-S", "colored-md")]
help:
    # ABSTRACT
    Interactive TUI for browsing and inspecting KDE Klipper clipboard history

    # DESCRIPTION
    `menu-clipboard` provides a fast, searchable interface for exploring the clipboard history managed by KDE Klipper.

    It solves the problem of having a large clipboard history that is difficult to navigate. It presents the history
    in a clean, interactive list with a powerful preview and actions for copying, inspecting, or clearing items.

    ## Key Features

    * **Fuzzy Search:** Instantly search through your entire clipboard history.
    * **Full-Content Preview:** The preview window displays the full, un-truncated content of the selected
      clipboard item, rendered with syntax highlighting via `bat`.
    * **Clipboard Actions:** Easily copy any item back to the clipboard, copy it to the primary selection, or
      clear the entire history.
    * **Clean & Composable:** Follows the three-mode pattern, allowing it to be used as an interactive TUI, a static
      table generator, or a pure JSON data source.

# => MVC ---------------------------------------------------------------------------------------------------------- {{{1

# Output clipboard history as a single JSON array of objects
model:
    #!/usr/bin/env -S bash -Eeuo pipefail
    busctl --user --json=short call org.kde.klipper /klipper org.kde.klipper.klipper -- getClipboardHistoryMenu \
        | jq -r '.data[] | to_entries | map({id: .key, value: .value})'

# Print clipboard history as a formatted and colorized table
view:
    #!/usr/bin/env -S bash -Eeuo pipefail
    jq_fields=(
        'id;\(.id)'
        'value;\([.value] | @tsv | if (length > 80) then .[0:80] + "…" end);silver'
    )
    "$this" model | "$json2table" 'map(select(.value | test("▨") | not)) | sort_by(.id)' "${jq_fields[@]}"

# Display the interactive TUI for browsing clipboard history
controller:
    #!/usr/bin/env -S bash -Eeuo pipefail
    export FZF_RELOAD_CMD="${this@Q} view"
    fzf_args=(
        --bind="alt-c:become(${this@Q} clear-history)"
        --bind="alt-enter:become(${this@Q} get-item-content {1}  | primarycopy &>/dev/null)"
        --bind="alt-i:execute(${this@Q} get-item-content {1} | bat --color always --style numbers,grid --paging always --wrap never)"
        --bind="alt-r:execute-silent(${this@Q} get-item-content {1} | polite)+reload($FZF_RELOAD_CMD)"
        --bind="alt-s:become(${this@Q} get-item-content {1} | secondarycopy &>/dev/null)"
        --bind="ctrl-m:become(${this@Q} get-item-content {1} | clipcopy &>/dev/null)"
        --bind="ctrl-y:become(${this@Q} get-item-content {1} | clipcopy &>/dev/null)"
        --footer='A-c:clear A-i:inspect A-enter:primary A-s:secondary enter:select | A-p:preview'
        --preview-window="right:40%:border-left:nowrap:nohidden"
        --preview="${this@Q} preview-item {1}"
        --prompt="clipboard history > "
    )
    "$this" view | "$fzf_table" "${fzf_args[@]}"

# => helper ------------------------------------------------------------------------------------------------------- {{{1

# Clear the clipboard history
[private]
@clear-history:
    busctl --user --json=short call org.kde.klipper /klipper org.kde.klipper.klipper -- clearClipboardHistory

# Get the raw content of a single clipboard item
[private]
get-item-content id:
    #!/usr/bin/env -S bash -Eeuo pipefail
    # busctl --user --json=short call org.kde.klipper /klipper org.kde.klipper.klipper -- getClipboardHistoryItem i "{{id}}"
    "$this" model | jq -jr --arg id "$id" "map(select(.id == $id))[].value"

# Generate a formatted preview for a single clipboard item
[private]
preview-item id:
    #!/usr/bin/env -S bash -Eeuo pipefail
    "$this" get-item-content "$id" | bat --color always --style=plain
