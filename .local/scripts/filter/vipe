#!/usr/bin/env perl

use v5.40;
use autodie;
use experimental qw(class declared_refs defer refaliasing);

use English      qw(-no_match_vars);
use File::Temp   qw(tempfile);
use Getopt::Long qw(:config auto_version bundling no_ignore_case);

local $INPUT_RECORD_SEPARATOR = undef;

my $suffix = '';
if (!GetOptions('suffix=s' => \$suffix)) {
    die "Usage: $0 [--suffix=extension]\n";
}
$suffix = ".$suffix" if $suffix =~ m/^[^.]/;

my ($fh, $tmp) = tempfile(UNLINK => 1, SUFFIX => $suffix);
$fh
    or die 'cannot create tempfile';

if (!-t STDIN) {
    print {$fh} <STDIN>
        or die "write temp: $!";
}

close $fh;
close STDIN;

open(STDIN, '<', '/dev/tty')
    or die "reopen stdin: $!";

open(my $out, '>&', \*STDOUT)
    or die "save stdout: $!";

close STDOUT;
open(STDOUT, '>', '/dev/tty')
    or die "reopen stdout: $!";

my @editor = ('vi');

if (exists $ENV{VISUAL}) {
    @editor = split(' ', $ENV{VISUAL});
} elsif (exists $ENV{EDITOR}) {
    @editor = split(' ', $ENV{EDITOR});
} elsif (-x '/usr/bin/editor') {
    @editor = '/usr/bin/editor';
}

my $ret = system(@editor, $tmp);
if ($ret != 0) {
    die "@editor exited nonzero, aborting\n";
}

open(my $in, '<', $tmp)
    or die "$0: cannot read $tmp: $!\n";

print {$out} <$in>
    or die "write failure: $!";

close $in;
close $out;

__END__

=head1 NAME

vipe - edit pipe

=head1 SYNOPSIS

command1 | vipe | command2

=head1 DESCRIPTION

vipe allows you to run your editor in the middle of a unix pipeline and
edit the data that is being piped between programs. Your editor will
have the full data being piped from command1 loaded into it, and when you
close it, that data will be piped into command2.

=head1 ARGUMENTS

vipe takes an argument --suffix that can be used to provide a file
extension to the temp file generated.  This enables editors to provide
syntax highlighting and activate modes.  For example, you can call vipe
like

  vipe --suffix csv

to create a tempfile with .csv extensions which makes Emacs (or your
favorite editor) launch in CSV major mode.

=head1 ENVIRONMENT VARIABLES

=over 4

=item EDITOR

Editor to use.

=item VISUAL

Also supported to determine what editor to use.

=back

=head1 AUTHOR

Copyright 2006 by Joey Hess <id@joeyh.name>

Licensed under the GNU GPL.

=cut
