#!/usr/bin/env -S perl -CALEO

use v5.36;

use Getopt::Long qw(:config auto_version bundling no_ignore_case);

use YAML::XS         qw();
use Cpanel::JSON::XS qw();
use experimental     qw(builtin);

use constant {
    'JSON'          => Cpanel::JSON::XS->new->canonical->utf8(0)->unblessed_bool([1])->pretty->space_before(0),
    'JSON_ONE_LINE' => Cpanel::JSON::XS->new->canonical->utf8(0)->unblessed_bool([1])->pretty(0)->indent(0),
};

my $inject_path = 0;
GetOptions('path|p' => \$inject_path)
    or die "Unable to process command line options\n";

my @data;
if (@ARGV) {
    @data = map {my $d = YAML::XS::LoadFile($_); $d->{'__path__'} = $_ if $inject_path; $d} @ARGV;
} else {
    my @lines;
    while (defined(my $line = <<>>)) {
        push @lines, $line;
    }

    my $input = join('', @lines);
    @data = YAML::XS::Load($input);
}

if (-t 1 && @data == 1) {
    print JSON()->encode($data[0]);
} else {
    say JSON_ONE_LINE()->encode($_) foreach @data;
}

__END__
