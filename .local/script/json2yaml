#!/usr/bin/env perl

use v5.36;
use open 'IO' => ':raw';

use YAML::XS         qw();
use Cpanel::JSON::XS qw();
use experimental     qw(try);

use constant {
    'COMMON_YAML_OPTS' => {
        footer                => 0,
        force_sequence_indent => 1,
        header                => 1,
        indent                => 2,
        utf8                  => 1,
        width                 => 120,
    },
};

use constant {
    'JSON' => Cpanel::JSON::XS->new->canonical->utf8(1)->unblessed_bool([1])->pretty->space_before(0)->allow_nonref(1),
    'YAML_WITH_FOOTER' => YAML::XS->new(COMMON_YAML_OPTS()->%*, footer => 1),
    'YAML'             => YAML::XS->new(COMMON_YAML_OPTS()->%*),
};

STDOUT->autoflush(1);

my $line = <<>>;

try {
    while (defined $line) {
        my $data = JSON()->decode($line);
        print YAML()->dump($data);
        $line = <<>>;
    }
} catch ($err) {
    my $input = join($/, $line, do {local $/ = undef; <<>>});
    my $data  = JSON()->decode($input);
    print YAML()->dump($data);
}

__END__
