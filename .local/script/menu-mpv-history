#!/usr/bin/env perl

use v5.36;
use autodie;
use open ':locale';
use utf8;

# Lua is a piece of shit, honestly, it even doesn't have any utf8 encoding/decoding in it's core library so it writes
# history strings in mpv "as is" in it's encoding, thus any title in history not properly encoded in utf8 (which is not
# a rare case for old video files for example) will warn on non-utf8 character and exit if such warning is fatal.
# Here we need to be ready to read the history file with non-utf8 characters written by Lua and that's why fatal utf8
# warnings are disable.
# Frankly speaking any language is as pathetic as it's core library pathetic is and Lua's core library is not even a joke.

# use warnings     qw(FATAL utf8);

use experimental qw(declared_refs refaliasing signatures);

use IPC::Open2 qw(open2);

use constant {
    DIR => "$ENV{XDG_STATE_HOME}/mpv",
    TSV => "$ENV{XDG_STATE_HOME}/mpv/play.history.tsv",
};

sub main {
    my %merge;
    foreach my $path (TSV()) {
        -r $path or next;

        my \%data = parse_file($path);
        @merge{keys %data} = values %data;
    }

    my @result = sort {$a->[0] cmp $b->[0]} values %merge;

    open(my $fhw, '>', TSV())
        or die sprintf("unable to open file: %s\n", TSV());
    say {$fhw} "localtime\ttype\ttitle\tcommand\tpath";
    say {$fhw} ($_->[2]) foreach @result;
    close($fhw);

    if (-t STDOUT) {
        my @cmd = (
            'fzf-execute',
            "--bind=ctrl-r:reload($0)",
            q{--bind=ctrl-u:execute-silent(echo -n {3} | perl -pE 'chomp; s/"(.+?)"/$1/g' | clipcopy)},
            sprintf('--bind=alt-r:execute-silent(grep -vF {3} %s | sponge %s)+reload(%s)', TSV(), TSV(), $0),
        );

        my $pid = open2('>&STDOUT', my $chld_in, @cmd);
        binmode($chld_in, ':encoding(UTF-8)');

        if (not defined $pid) {
            say {*STDERR} 'failed to execute fzf';
            return 1;
        }

        say {$chld_in} ($_->[1]) foreach reverse @result;
        close $chld_in;

        waitpid($pid, 0)
            or die "unable to wait for child to finish: $!\n";
    } else {
        say {*STDOUT} ($_->[1]) foreach reverse @result;
    }

    return 0;
}

sub parse_file($path) {
    open(my $fhr, '<', $path)
        or die "unable to open file: $path\n";

    my $result;
    if ($path =~ m/[.]tsv$/) {
        $result = parse_tsv($fhr);
    }
    close($fhr);

    return $result;
}

sub parse_tsv($fh) {
    my %result;

    my $header = <$fh>;
    while (my $line = <$fh>) {
        chomp($line);
        my ($localtime, $type, $title, $cmd, $path) = split m/\t/, $line;
        unless ($localtime && $cmd && $path) {
            die "unable to parse line: $line";
        }
        $path =~ s/['"](.+?)['"]/"$1"/g;

        my $key = $type . $path;
        $result{$key} = [
            $localtime,
            sprintf("%s: %s\t%s\t%s",     $type, $title, $cmd, $path),
            sprintf("%s\t%s\t%s\t%s\t%s", $localtime, $type, $title, $cmd, $path),
        ];
    }

    return \%result;
}

exit main();

__END__
