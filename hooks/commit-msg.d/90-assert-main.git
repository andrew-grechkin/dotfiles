#!/usr/bin/env perl

use v5.36;
use autodie;
use open ':locale';
use utf8;
use warnings     qw(FATAL utf8);
use experimental qw(declared_refs refaliasing);

use Env            qw($GIT_DIR $GIT_URL $GIT_WORK_TREE);
use File::Basename qw(basename);
use Sub::Util      qw(set_subname);

my $match = qr/main[.]git$/;
my $name  = basename($GIT_URL);

$name =~ $match
    or exit 0;

my $message = do {local $/ = undef; open my $fh, '<', $ARGV[0]; <$fh>};

my @validators = (
    set_subname(
        'assert-ticket',
        sub() {
            my $topic = 'DEP';
            my $regex = qr{ \[ $topic- \d+ \] }xms;
            if ($message !~ $regex) {
                die "[POLICY] Your message is not formatted correctly. Expected [${topic}-xxx] ticket reference\n";
            }
            return;
        },
    ),
);

$_->() foreach @validators;

exit 0;

__END__
